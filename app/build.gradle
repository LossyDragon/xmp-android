apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

// https://developer.android.com/ndk/downloads
android {
    compileSdkVersion 29
    ndkVersion "21.4.7075529"
    defaultConfig {
        applicationId "org.helllabs.android.xmp"
        minSdkVersion 21
        //noinspection OldTargetApi
        targetSdkVersion 29

        externalNativeBuild.cmake {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }

        // ModArchive API Key
        // Must be in your global gradle.properties. ex: C:\Users\<name>\.gradle
        buildConfigField("String", "API_KEY", "$modArchiveApiKey")
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        ndkBuild.path 'src/main/cpp/Android.mk'
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Included .jars
    implementation files('libs/groundy-1.5.jar')

    // Android Support Libs
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // Other Libs
    implementation 'com.android.volley:volley:1.1.1'
    implementation('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.8.5@aar') {
        transitive = true
    }
    implementation 'com.fnp:material-preferences:0.1.4'
    implementation 'com.github.pluscubed:recycler-fast-scroll:0.3.0@aar'
}

// Completely remove libxmp folder and clone a new copy.
task getLibxmp(type: Exec) {
    workingDir '/app/src/main/cpp'
    commandLine 'bash', '-c', 'rm -r libxmp && git clone https://github.com/libxmp/libxmp.git && exit'
}

// Build libxmp's object files
task buildLibXmp(type: Exec) {
    def args = "autoconf && ./configure && make && make check && (cd test-dev; autoconf && ./configure && make) && exit"
    workingDir '../app/src/main/cpp/libxmp'
    commandLine 'bash', '-c', args
}
