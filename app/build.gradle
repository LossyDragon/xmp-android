apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: "org.jlleitschuh.gradle.ktlint"

// https://developer.android.com/ndk/downloads
android {
    compileSdkVersion 30
    ndkVersion "23.0.7123448-beta1"

    defaultConfig {
        applicationId "org.helllabs.android.xmp"
        minSdkVersion 21
        //noinspection OldTargetApi
        targetSdkVersion 29

        versionCode = 90
        versionName = "4.15.0"

        externalNativeBuild.ndkBuild {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }

        // ModArchive API Key
        // Must be in your global gradle.properties. ex: C:\Users\<name>\.gradle
        buildConfigField("String", "API_KEY", "$modArchiveApiKey")

        // Pretty print compiled apk with version into and date
        applicationVariants.all { variant ->
            variant.outputs.all {
                def date = new Date()
                def formattedDate = date.format('YYYYMMdd')
                outputFileName = "xmp-${versionName}-${versionCode}-${formattedDate}.apk"
            }
        }
    }

    buildTypes {
        debug {
            jniDebuggable true
            externalNativeBuild {
                ndkBuild.cFlags "-DDEBUG=1"
            }
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        ndkBuild.path 'src/main/cpp/Android.mk'
    }

    // Hush: ExperimentalCoroutinesApi
    // Then: ExperimentalFoundationApi
    kotlinOptions {
        jvmTarget = "1.8"
        useIR = true
        freeCompilerArgs += "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi"
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }

    // Removes: Coroutines debug artifact
    packagingOptions {
        exclude "DebugProbesKt.bin"
    }

    // View binding
    buildFeatures {
        compose true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Compose
    implementation 'androidx.compose.ui:ui:1.0.0-beta04'
    // Tooling support (Previews, etc.)
    implementation 'androidx.compose.ui:ui-tooling:1.0.0-beta04'
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation 'androidx.compose.foundation:foundation:1.0.0-beta04'
    // Material Design
    implementation 'androidx.compose.material:material:1.0.0-beta04'
    // Material design icons
    implementation 'androidx.compose.material:material-icons-core:1.0.0-beta04'
    implementation 'androidx.compose.material:material-icons-extended:1.0.0-beta04'
    // Constraint
    implementation 'androidx.constraintlayout:constraintlayout-compose:1.0.0-alpha05'
    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.3.0-alpha05'
    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:1.0.0-alpha03'
    // Live Data
    implementation 'androidx.compose.runtime:runtime-livedata:1.0.0-beta04'
    // Accompanist - System UI Controller
    implementation 'com.google.accompanist:accompanist-systemuicontroller:0.7.0'
    // Accompanist - Insets
    implementation "com.google.accompanist:accompanist-insets:0.7.0"

    // Android Support Libs
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation "androidx.media:media:1.2.1"

    // AIDL-like replacement.
    implementation 'org.greenrobot:eventbus:3.2.0'

    // Dep Injection
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03"
    implementation "com.google.dagger:hilt-android:$hilt_android"
    kapt "com.google.dagger:hilt-compiler:$hilt_android"

    // Coroutines
    def coroutines = "1.4.3"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"

    // Http & Download
    implementation 'androidx.tonyodev.fetch2:xfetch2:3.1.6'
    implementation "androidx.tonyodev.fetch2okhttp:xfetch2okhttp:3.1.6"
    implementation "com.squareup.okhttp3:okhttp:4.9.0"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"

    // Retrofit XML Parsing
    def tikXml = '0.8.13'
    implementation("com.tickaroo.tikxml:annotation") { version { strictly "$tikXml" } }
    implementation("com.tickaroo.tikxml:retrofit-converter") { version { strictly "$tikXml" } }
    kapt("com.tickaroo.tikxml:processor") { version { strictly "$tikXml" } }

    // Other Libs
    implementation 'com.squareup.moshi:moshi-kotlin:1.11.0'
    implementation 'com.afollestad.material-dialogs:core:3.3.0'
    implementation 'com.afollestad.material-dialogs:lifecycle:3.3.0'

    // LeakCanary
    // debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.6'
}

ktlint {
    android = true
    ignoreFailures = true
    // Ignore: wildcard imports
    disabledRules = ["no-wildcard-imports"]
}

// Run KtLint checker
task runKtlintCheck(type: Exec) {
    workingDir '../'
    commandLine 'cmd', '/c', 'gradlew ktlintCheck'
}

// Run KtLint formatter
task runKtlintFormat(type: Exec) {
    workingDir '../'
    commandLine 'cmd', '/c', 'gradlew ktlintFormat'
}

// Completely remove libxmp folder and clone a new copy.
task getLibxmp(type: Exec) {
    def args = "rm -rf libxmp && git clone https://github.com/libxmp/libxmp.git && exit"
    workingDir '../app/src/main/cpp'
    commandLine 'bash', '-c', args
}

// Build libxmp's object files
task buildLibXmp(type: Exec) {
    def args = "autoconf && ./configure && make && make check && (cd test-dev; autoconf && ./configure && make) && exit"
    workingDir '../app/src/main/cpp/libxmp'
    commandLine 'bash', '-c', args
}
